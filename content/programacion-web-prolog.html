<p>Como ya hemos visto con anterioridad, <a href="../../introduccion-a-prolog-tutorial-en-espanol">Prolog</a> es un muy buen lenguaje para ciertos problemas. Hoy en d&iacute;a, el lenguaje que se cre&iacute;a que ser&iacute;a el futuro est&aacute; pr&aacute;cticamente olvidado. No obstante, existen algunas personas que se han esforzado para que Prolog siga siendo un lenguaje &uacute;til. &iquest;Y qu&eacute; requisito es b&aacute;sico para un lenguaje &uacute;til? &iexcl;Que pueda usarse para hacer un servidor web claro! Es por eso que decid&iacute; emprender una aventura quijotesca y ver hasta donde era capaz de llegar Prolog en el desarrollo web. Para ello voy a usar <a href="http://www.swi-prolog.org/">SWI Prolog.</a>&iquest;Suena alocado? Pues ya lo est&aacute;s usando, el servidor de gesti&oacute;n de im&aacute;genes de Adrianist&aacute;n (https://files.adrianistan.eu) est&aacute; hecho en Prolog.</p>
<h2>Handlers y configuraci&oacute;n</h2>
<p>Como todo buen web framework, en este debe poder configurarse unas rutas (handlers) y algunos par&aacute;metros tales como el puerto, n&uacute;mero de hilos (Prolog por defecto usar&aacute; varios hilos para manejar las peticiones web en paralelo) y m&aacute;s cosas. Este c&oacute;digo es el m&aacute;s simple que nos permite acceder a una p&aacute;gina y mostrar un hola mundo.</p>
<pre><code class="language-prolog">
:- use_module(library(http/thread_httpd)).
:- use_module(library(http/http_dispatch)).
:- use_module(library(http/html_write)).
:- use_module(library(http/http_unix_daemon)).


:- http_handler(/,index,[]).

index(_Request) :-
    format('Content-Type: text/plain~n~n'),
    format('Hola Mundo').

:- http_daemon([port(4777),fork(false)]).
</code></pre>
<p>Si ejecutamos con SWI Prolog, veremos que el servidor arranca:</p>
<p style="text-align: center;"><img src="https://files.adrianistan.eu/PrologWebHelloWorld.png" alt="" width="1366" height="768" /></p>
<p style="text-align: left;">Como vemos, el c&oacute;digo es super simple. Centr&eacute;monos m&aacute;s en la parte de los handlers. Es tan simple que parece que faltan cosas, &iquest;c&oacute;mo definir rutas con par&aacute;metros? &iquest;y la distinci&oacute;n entre GET y POST? Todo es muy sencillo como veremos.</p>
<p style="text-align: left;">El predicado <strong>http_handler</strong> tiene aridad 3. En primer lugar la ruta, en segundo lugar el predicado que responder&aacute; a la petici&oacute;n y en tercer lugar una lista con las opciones.</p>
<p style="text-align: left;">En primer lugar las rutas se definen con algo denominado <strong>FileSpec</strong>, que es una construcci&oacute;n de Prolog que permite expresar rutas relativas a partir de un alias absoluto. No solo se usa aqu&iacute;, sino en todas partes. F&iacute;jate en como usamos <strong>use_module</strong>. Tenemos library y dentro una ruta. library representa un path absoluto con ese alias y dentro se representa el resto de la ruta. Otro alias predefinido y que podemos usar en el contexto de una aplicaci&oacute;n web es <strong>root</strong>. root(.) es equivalente a /. Si queremos soportar paths variables, &iexcl;usamos variables de Prolog! Luego nos aseguramos que que esas variables tambi&eacute;n esten presentes en el predicado de la respuesta.</p>
<p style="text-align: left;">Aqu&iacute; alguien se puede extra&ntilde;ar ya que este predicado no tiene la misma aridad arriba que abajo, <em>aparentemente</em>. En realidad como http_handler siempre a&ntilde;ade la variable Request, Prolog no tiene ning&uacute;n problema.</p>
<pre><code class="language-prolog">
:- use_module(library(http/thread_httpd)).
:- use_module(library(http/http_dispatch)).
:- use_module(library(http/html_write)).
:- use_module(library(http/http_unix_daemon)).


:- http_handler(/,index,[]).
:- http_handler(root(user/User),user(User),[method(get)]).

index(_Request) :-
    format('Content-Type: text/plain~n~n'),
    format('Hola Mundo').

user(User,_Request) :-
    format('Content-Type: text/plain~n~n'),
    format('Usuario: '),
    format(User).

:- http_daemon([port(4777),fork(false)]).
</code></pre>
<p style="text-align: left;">Por otro lado en las opciones podemos definir gran variedad de ajustes, entre ellos el m&eacute;todo de la petici&oacute;n. Como bien es sabido en Prolog, Prolog unifica, &iexcl;incluso en una aplicaci&oacute;n web! As&iacute; que la clave es cuanto m&aacute;s expl&iacute;citos seamos, en menos ocasiones unificar&aacute; el handler.</p>
<p style="text-align: center;"><img src="https://files.adrianistan.eu/PrologWebParameter.png" alt="" width="1366" height="768" /></p>
<h2 style="text-align: left;">Ficheros, HTML y JSON</h2>
<p>Ya hemos visto como funcionan los handlers pero hemos estado generando las respuestas con unas llamadas a format un poco cutres. Por fortuna, Prolog nos hace la vida m&aacute;s f&aacute;cil si vamos a devolver un fichero, un HTML din&aacute;mico o un archivo JSON.</p>
<p>Para ficheros, es trivial, simplemente respondemos con el predicado <strong>http_reply_file</strong>.</p>
<p>Para HTML hay dos maneras de hacerlo. Una es usando el DSL, otra es usando plantillas. Aqu&iacute; vamos a ver el DSL ya que es m&aacute;s estilo Prolog. Para responder usando este m&eacute;todo usamos el predicado <strong>reply_html_page</strong>. Este toma un head y un body. Este DSL es muy intuitivo y permite escribir estructuras anidadas as&iacute; como atributos. Este DSL adem&aacute;s permite ser usado con DCG, as&iacute; que se puede escribir en un predicado aparte y se pueden hacer includes de forma c&oacute;moda. Adem&aacute;s se pueden pasar variables, aunque en este ejemplo no lo hago.&nbsp;</p>
<p>Por &uacute;ltimo, para responder un JSON usamos el predicado <strong>reply_json</strong>. SWI Prolog admite varias sintaxis para representar JSON. Podemos usar dentro un t&eacute;rmino json la sintaxis Clave-Valor, Clave=Valor o Clave(Valor). Esta &uacute;ltima es la que he usado. Adem&aacute;s a partir de SWI Prolog 7 se pueden usar los diccionarios de Prolog tambi&eacute;n.</p>
<p>Finalmente aqu&iacute; un ejemplo con los tres predicados en uso:</p>
<pre><code class="language-prolog">
:- use_module(library(http/thread_httpd)).
:- use_module(library(http/http_dispatch)).
:- use_module(library(http/html_write)).
:- use_module(library(http/http_json)).
:- use_module(library(http/http_unix_daemon)).


:- http_handler(/,index,[]).
:- http_handler(root(user/User),user(User),[method(get)]).
:- http_handler('/foto.jpg',static,[]).
:- http_handler('/api',api,[]).

index(_Request) :-
    reply_html_page(
        title('Prolog WebApp'),
        [\index_body]).

index_body --&gt;
    html([
        h1('Prolog WebApp'),
        p([style('color: red'),id('parrafo')],'Hola Mundo'),
        ul([
            li('Alonso Quijana'),
            li('Sancho Panza')   
        ])
    ]).

api(_Request) :-
    reply_json(json([
       name('Alonso Quijana'),
       email('quijana@mail.xp') 
    ])).

user(User,_Request) :-
    format('Content-Type: text/plain~n~n'),
    format('Usuario: '),
    format(User).

static(Request) :-
    http_reply_file('burgos.jpeg',[],Request).

:- http_daemon([port(4777),fork(false)]).
</code></pre>
<h2>Peticiones POST</h2>
<p style="text-align: left;">Vamos a ver como leer esos formularios desde Prolog. En primer lugar para obtener un listado de todas las variables POST usamos el predicado <strong>http_read_data</strong>. Despu&eacute;s simplemente buscamos en la lista con el archiconocido predicado&nbsp;<strong>member</strong>, unificando con una variable libre para obtener ya el dato directamente.</p>
<pre><code class="language-prolog">
form(Request) :-
    http_read_data(Request,Data,[]),
    member(email=Email,Data),
    format('Content-Type: text/plain~n~n'),
    format('El correo indicado es: '),
    format(Email).
</code></pre>
<h2 style="text-align: left;">Autenticaci&oacute;n Basic</h2>
<p>Un ejemplo m&aacute;s elaborado es la autenticaci&oacute;n HTTP. Aqu&iacute; defino un predicado auth que va a contener la validaci&oacute;n.</p>
<pre><code class="language-prolog">
auth(Request) :-
    Realm = 'Prolog WebApp',
    (
        string_codes("123456789",Password),
        member(authorization(Header),Request),http_authorization_data(Header,basic(admin,Password)) -&gt; true
        ;
        throw(http_reply(authorise(basic, Realm)))
    ).
</code></pre>
<p>Para usarla, simplemente hay que poner el predicado auth en la primera l&iacute;nea de cualquier predicado de handler.</p>
<h2>Conclusiones</h2>
<p>El c&oacute;digo final est&aacute; en el repositorio de <a href="https://github.com/aarroyoc/blog-ejemplos">ejemplos del blog</a>. Respecto a la base de datos. No lo he comentado porque ser&iacute;a otro asunto, adem&aacute;s Prolog ya es una base de datos gracias a assert y retract. SWI Prolog incluye adem&aacute;s una muy buena implementaci&oacute;n de RDF. Espero que con este post os haya abierto la mente a un tipo de programaci&oacute;n diferente, la l&oacute;gica, que no solo sirve para resolver problemas "raros" sino para aplicaciones web de forma extremadamente sencilla (&iexcl;una vez comprendamos Prolog claro!). Se trata de un enfoque muy interesante y con el que se puede llegar a ser muy productivo. Yo me esperaba algo mucho peor, pero la gente detr&aacute;s de SWI Prolog se ha esforzado para que todo sea simple y claro.</p>
<p style="text-align: left;">&nbsp;</p>
<p style="text-align: left;">&nbsp;</p>