<p>Que en este blog se habla de Prolog es algo que se da por hecho. Hoy sin embargo, vamos a hablar de un lenguaje quizá más desconocido que extiende Prolog. Se trata de <a href="https://logtalk.org/">Logtalk</a> y de forma resumida podríamos decir que es Prolog + Orientación a Objetos. Pero no cualquier tipo de orientación a objetos. Orientación a objetos <i>declarativa</i>. Logtalk nos permite encapsular y reutilizar código de forma mucho más efectiva que en Prolog, no perdiendo nada de lo que ya podemos hacer en Prolog.</p>

<div style="text-align:center">
<img src="https://files.adrianistan.eu/LogtalkLogo.png">
</div>

<p>Logtalk nació en 1998 debido a los problemas de Prolog al escribir aplicaciones grandes. Diseñado por Paulo Moura, se trata de acercar Smalltalk y Prolog. De este modo el elemento fundamental de Logtalk serán los objetos, no las clases. A través de objetos podremos hacer clases si es lo que deseamos, o podemos usarlos como prototipos. Logtalk se ejecuta por encima de un sistema Prolog, sobre todo aquellos que implementen el estándar ISO Prolog. Actualmente Logtalk soporta: B-Prolog, Ciao Prolog, CxProlog, ECLiPSe, GNU Prolog, JIProlog, LVM, Quintus Prolog, Scryer Prolog, SICStus Prolog, SWI Prolog, Tau Prolog, Trealla Prolog, XSB y YAP. El código 100% Logtalk debería ser portable entre estos sistemas. En este artículo usaré <a href="https://github.com/mthom/scryer-prolog">Scryer Prolog</a>. Podemos instalar Logtalk desde <a href="https://logtalk.org/">logtalk.org</a></p>
<p>En Logtalk existen tres tipos de entidades: objetos, protocolos y categorías.</p>
<h2>Objetos</h2>
<p>El elemento fundamental de Logtalk es el objeto. Veremos que casi todo en Logtalk son objetos. Estos objetos no tienen estado ya que son puramente declarativos. Con los objetos podemos construir jerarquías tipo prototipo (ideal para objetos únicos) y tipo clase (ideal para objetos de los que se harán muchos).</p>

<pre><code class="language-prolog">
:- object(python).

    :- public(say_creator/0).
    say_creator :-
        write('Created by: Guido van Rossum'),nl.

:- end_object.
</code></pre>
<p>Este código crea un objeto llamado <i>python</i> con un predicado o método público, llamado <i>say_creator</i> con aridad 0. El predicado en sí lo que hace es imprimir por pantalla un texto. Lo vamos a guardar en un fichero llamado langs.lgt (lgt es la extensión de Logtalk)</p>

<p>Vamos a ejecutarlo. Para ello, lanzamos el comando de Logtalk de nuestro Prolog, en mi caso, es <i>scryerlgt</i>. Si usas SWI Prolog, es <i>swilgt</i>. Si todo va bien, verás una terminal similar a la de Prolog. Una vez allí, cargamos el fichero con <i>logtalk_load</i>.</p>
<pre><code>
?- logtalk_load('langs.lgt').
</code></pre>
<p>Si todo va bien, ahora podremos mandar un mensaje al objeto python.</p>
<pre><code>
?- python::say_creator.
</code></pre>
<div style="text-align:center">
<img src="https://files.adrianistan.eu/Logtalk1.png" width="500"/>
</div>
<p>Si no existe predicado en el objeto o si la visibilidad del método no lo permite, el envío falla. Se podría decir mucho de los mensajes como que emiten eventos, se puede hacer broadcasting, etc pero esto es más avanzado.</p>

<p>Los objetos pueden: <i>implementar</i> protocolos, <i>importar</i> categorías, <i>extender</i> otros objetos, <i>instanciar</i> un objeto (clase) y <i>especializar</i> otro objeto (clase). Las jerarquías de objetos se pueden realizar mediante extensiones (prototipos) o mediante instancias y especializaciones (clases).</p>
<p>Por ejemplo, un objeto que <i>extienda</i> otro se haría así:</p>
<pre><code class="language-prolog">
:- object(animal).

    :- public(run/0).
    run :-
        write('Animal is running'),nl.

:- end_object.

:- object(dog, extends(animal)).

    :- public(sleep/0).
    sleep :-
        write('Dog is sleeping'),nl.
:- end_object.
</code></pre>
<p>Si cargamos el fichero, veremos que el objeto animal tiene solo el método run, mientras que dog tiene el método run y sleep.</p>

<p>Aquí podemos introducir también las otras dos formas de mandar un mensaje. Con <i>::</i> podemos mandar un mensaje al mismo objeto en el que estamos. Y con <i>^^</i> podemos mandar un mensaje al superobjeto (es decir, del que estamos extendiendo).</p>

<pre><code class="language-prolog">
:- object(animal).

    :- public(run/0).
    run :-
        write('Animal is running'),nl.

:- end_object.

:- object(dog, extends(animal)).

    :- public(run/0).
    run :-
        write('Dog is running'),nl,
        ^^run.

    :- public(sleep/0).
    sleep :-
        write('Dog is sleeping'),nl.

    :- public(run_and_sleep/0).
    run_and_sleep :-
        ::run,
        ::sleep.
:- end_object.
</code></pre>
<p>Al ejecutar <i>dog::run_and_sleep/0</i> obtendremos:</p>
<pre><code>
Dog is running
Animal is running
Dog is sleeping
</code></pre>
<p>Adicionalmente podemos usar <i>self/1</i> para obtener una referencia al objeto actual. <i>run_and_sleep/0</i> puede escribirse también así:</p>
<pre><code class="language-prolog">
    run_and_sleep :-
        self(Self),
        Self::run,
        Self::sleep.
</code></pre>

<h3>Crear objetos de forma dinámica</h3>
<p>Si has programado en otros lenguaje OOP quizá te esté extrañando que todos los objetos existan desde el principio en Logtalk. Afortunadamente, esto era porque no habíamos llegado todavía a la parte donde se crean de forma dinámica. Esto se hace mediante el predicado <i>create_object/4</i>.</p>
<p>El primer argumento será la variable o el nombre del objeto. Seguidamente irán las relaciones. En tercer lugar las directivas (ahora las vemos pero básicamente public, private, info,...) y en cuarto lugar las cláusulas.</p>
<p>Por ejemplo, estos dos códigos son equivalentes:</p>
<pre><code class="language-prolog">
?- create_object(foo, [extends(bar)], [public(foo/1)], [foo(1), foo(2)]).

%%%%

:- object(foo, extends(bar)).

    :- dynamic.

    :- public(foo/1).
    foo(1).
    foo(2).

:- end_object.
</code></pre>
<p>Lo normal es dejar la creación de objetos dinámicos en un objeto prototito o en una metaclase.</p>
<p>Los objetos dinámicos se pueden borrar con <i>abolish_object/1</i>.</p>

<h3>Objetos paramétricos</h3>
<p>Existen unos objetos algo diferentes, los objetos paramétricos. Son objetos cuyo identificador no es un átomo sino un término compuesto de Prolog. De este modo los propios objetos cargan con información en su definición de nombre.</p>

<pre><code class="language-prolog">
:- object(ruby(_Creator)).
    
    :- public(say_creator/0).
    say_creator :-
        write('Created by: '),
        parameter(1, Creator),
        write(Creator),nl.
        
:- end_object.
</code></pre>
<p>Y lo tendremos que ejecutar de esta forma: <i>ruby('Matz')::say_creator.</i>. Los objetos paramétricos también pueden usar <i>this/1</i> y la sintaxis de doble underscore (_Creator_). Pero como siempre, para estas cosas extra, lo recomendable es mirar el Logtalk Handbook.</p>

<h2>Protocolos</h2>
<p>Lo siguiente dentro de Logtalk son los protocolos. Los protocolos permiten separar la interfaz de la implementación. Los protocolos solo incluyen las declaraciones de los predicados, pero no su lógica.</p>
<pre><code class="language-prolog">
:- protocol(langp).

    :- public(say_creator/0).

:- end_protocol.

:- object(python, implements(langp)).

    say_creator :-
        write('Created by: Guido van Rossum'),nl.

:- end_object.
</code></pre>
<p>Ahora definimos un protocolo llamado langp, que el objeto python implementa. Ahora ya no necesitamos incluir la directiva de visibilidad en el objeto ya que esa forma parte de la interfaz al exterior del objeto.</p> 

<h2>Categorías</h2>
<p>Las categorías son unidades de código reutilizables. Podemos pensar en ellos como partes de un objeto que se pueden añadir a otros objetos.</p>
<pre><code class="language-prolog">
:- category(reverse_creator(_Creator_)).

    :- uses(list, [reverse/2]).

    :- public(reverse_creator/1).
    reverse_creator(Reversed) :-
        reverse(_Creator_, Reversed).

:- end_category.

:- object(ruby(CreatorParam), implements(langp), imports(reverse_creator(CreatorParam))).
    
    say_creator :-
        write('Created by: '),
        parameter(1, Creator),
        write(Creator),nl.
        
:- end_object.
</code></pre>
<p>En este ejemplo, vamos a tener una categoría (paramétrica) y vamos a importarlo desde nuestro objeto paramétrico. Así podemos ver como los objetos paramétricos se pueden pasar los parámetros entre sí. Además, en el ejemplo se usa la directiva use, que nos permite "importar" ciertos predicados de algunos objetos (en este caso del objeto list) para no tener que escribir la sintaxis larga de mandar mensaje.</p>
<p>Para que esto funcione, vamos a tener que cargar la librería con el objeto list en Logtalk primero.</p>
<pre><code class="language-prolog">
?- logtalk_load(types(loader)).
    ....
?- logtalk_load('langs.lgt').
    ....
?- ruby("Matz")::reverse_creator(X).
    X = "ztaM".
</code></pre>
<p>Esto nos lleva a la pregunta de, ¿hay alguna forma de organizar los proyectos en Logtalk?</p>

<h2>Estructura proyecto Logtalk</h2>
<p>Por norma general un proyecto Logtalk consta de archivos lgt. Hay dos especiales, loader.lgt y tester.lgt.</p>
<p>loader.lgt deberá contener todo lo necesario para cargar la aplicación, normalmente cargar todos los ficheros y librerías en el orden correcto.</p>
<pre><code class="language-prolog">
:- initialization((
    logtalk_load(sets(loader)),
    logtalk_load(meta(loader)),
    logtalk_load(app)
)).
</code></pre>
<p>Este es un ejemplo de fichero loader, que carga las librerías sets, meta y el fichero app.lgt.</p>
<p>El fichero tester.lgt es igual, pero deberá incluir una llamada a ejecutar los tests de lgtUnit. lgtUnit es el framework de testing de Logtalk, muy interesante pero del que hablaré en otro artículo.</p>

<h2>Lambdas y más</h2>
<p>Logtalk incluye predicados lambda, es decir, predicados definidos sobre un metapredicado.</p>
<pre><code class="language-prolog">
?- meta::map([X, Y]>>(Y is 2*X), [1,2,3], Ys).
   Ys = [2,4,6].
</code></pre>
<p>En este caso, el predicado lambda es equivalente al predicado:</p>
<pre><code class="language-prolog">
times2(X, Y) :- Y is 2 * X.
</code></pre>
<p>Pero sin tener que haber definido un nombre ni ubicarlo en otro sitio.</p>

<p>Logtalk incluye más funcionalidad aparte de objetos, protocolos y categorías. Eventos, multithreading, type checking, debugger, documentation, tests, etc. Definitivamente, escribir sobre todo ello llevaría un buen rato.</p>
<p>Este artículo solo pretende ser una idea introductoria a Logtalk. Aparte del handbook también puedes ver los <a href="https://github.com/LogtalkDotOrg/logtalk3/">ejemplos del repo de GitHub</a> o la web de <a href="https://learnxinyminutes.com/docs/logtalk/">Learn X in Y minutes</a></p>


